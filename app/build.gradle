apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.example.moviesawesome"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file("$rootDir/distribution/debug/app-free-debug.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }

        release {
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
            storeFile file("$rootDir/distribution/release/app-free-release.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            useProguard false
            shrinkResources false
            debuggable true
            zipAlignEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            useProguard true
            debuggable false
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        free {
            applicationId "com.example.moviesawesome.free"
            versionCode 1
            versionName "1.0"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            dimension "default"
        }

        paid {
            applicationId "com.example.moviesawesome.paid"
            versionCode 1
            versionName "1.0"
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':main')
    implementation project(':detail')
    testImplementation project(':test')
    androidTestImplementation project(':test')
}
